/*
* ------------------------------------------------------------------------------------------ 
* ALS4RPA - EXPERIMENT C V1
* ------------------------------------------------------------------------------------------  
 
 Specification with the ITLingo ASL V.1.3 language   
 
 Authors: Carlos Correia and Alberto Rodrigues da Silva (Supervisor)  
 
 Created Date: 2022/February 
 Last Update:  2022/June      
 
 Copyright  --------- 
 2016-2022 Alberto Rodrigues da Silva (coordinator) et al., Instituto Superior Técnico, 
 Universidade de Lisboa. All rights reserved.  
 
 This material is available for academic, research and commercial purpose, however, You 
 should keep the authors and copyright notice.  
 Any use of this software constitutes full acceptance of all terms of the software's license.    
 
 Licence 
 ------- 
 
 This material is released under the Eclipse Public License (EPL) - Version 1.0. 
 http://www.eclipse.org/legal/epl-v10.html    
 
 General Information: ----------------- 

*/

Package ASL4RPA
       
Import ASL4RPALibraries.*  
         
System ExperimentC : Application [
	description 
	" Experiment C illustrates a web store application for purchasing clothing products 
    that need to test. In this sense, this automation intends to develop a test bench for 
    the web application and, in this way, ensure that the application meets the requirements 
    and has as few bugs as possible (i.e., automatically supporting acceptance tests).
	This scenario comprises essential six tasks:
	•	Case C - T1: Login to the web portal by introducing a username and password.
	•	Case C - T2: Add items to the shopping cart and check if the quantity of products 
                     is updated and accurate.
	•	Case C - T3: Check out the shopping cart and check if you get the text message 
                     Your order on My Store is complete.
	•	Case C - T4: Click on a product and add it to a Wishlist, then click on that 
                     Wishlist products button and check if the newly added item is on that list.
	•	Case C - T5: Click on an article and make a comment review. Then, check for the message 
                     Your comment has been added.
	•	Case C - T6: Search for a product by name or description and verify that the article's 
                     quantity is correct.
	This Web Store Application corresponds to fake clothes online shop site used to practice 
    test automation and encompasses the entire online shopping roadmap."]     
   
Robot SignIn [description "As a Visitor I want to sign in" ] 
begin         
	variable @users : DataTable definedAs     
	[attribute firstName : String,          
	 attribute password : String,
	 attribute email : String]   
	:=                 
	[[firstName, password    , email             ]
	 ["Joe"    , "mypassword", "west88@email.com"]
	 ["Mary"   , "mypassword", "mary@email.com"  ]]
		 
	UIWeb.OpenBrowser ("http://automationpractice.com/index.php") 
	UIWeb.ClickOnElement ("css:a.login")          
	UIWeb.PostData ("css:#email", @users.email[2])
	UIWeb.PostDataSecret ("css:#passwd", @users.password[2])    
	UIWeb.ClickOnButton ("css:#SubmitLogin")    
	UIWeb.CheckValueOnElement ("css:#header > div.nav > div > div > nav > div:nth-child(1) > a > span",@users.firstName[2])  	
endRobot        
 
Robot AddToCart [description "As a User I want to add products to my cart" ]
begin     
 	variable quantity : Integer := 1
 
	UIWeb.ClickOnElement ("css:img.logo") 
	UIWeb.ClickOnElement ("//*[@id=\"homefeatured\"]/li[1]/div/div[1]/div/a[1]/img")
	UIWeb.ClickOnElement ("//*[@id=\"add_to_cart\"]/button/span")
	UIWeb.ClickOnElement ("css:img.logo") 
	UIWeb.ClickOnElement ("//*[@id=\"homefeatured\"]/li[2]/div/div[1]/div/a[1]/img")
	UIWeb.ClickOnElement ("//*[@id=\"add_to_cart\"]/button/span")
    quantity := [UIWeb.GetData ("//*[@id=\"header\"]/div[3]/div/div/div[3]/div/a/span[1]")] 
endRobot 
 
Robot CheckOut [description "As a User I want to check out my shopping cart so that I can get my items" ]
begin    
	UIWeb.ClickOnElement ("css:div.shopping_cart > a") 
	UIWeb.ClickOnElement  ("//*[@id=\"center_column\"]/p[2]/a[1]/span")
	UIWeb.ClickOnButton ("//*[@id=\"center_column\"]/form/p/button") 
	UIWeb.SelectCheckbox ("//*[@id=\"cgv\"]")
	UIWeb.ClickOnElement ("//*[@id=\"form\"]/p/button")
	UIWeb.ClickOnElement ("//*[@id=\"HOOK_PAYMENT\"]/div[1]/div/p/a")
	UIWeb.ClickOnElement ("//*[@id=\"cart_navigation\"]/button")
	UIWeb.CheckValueOnElement ("//*[@id=\"center_column\"]/div/p/strong", "Your order on My Store is complete.")
endRobot    
  
Robot Wishlist [description "As a User I want to add products to my wishlist so that other users are aware of the products that I want" ]
begin  
 	variable product : Integer := 1 
    
	UIWeb.ClickOnElement ("css:img.logo") 
	UIWeb.ClickOnElement ("//*[@id=\"homefeatured\"]/li[1]/div/div[1]/div/a[1]/img")
	product := [UIWeb.GetData ("//*[@id=\"center_column\"]/div/div/div[3]/h1")]
	UIWeb.ClickOnElement ("//*[@id=\"wishlist_button\"]") 
endRobot
  
Robot Review [description "As a User I want to write a review so that the other users know my opinion" ]
begin      	   
	variable @review : DataTable definedAs  
	[attribute title : String,
	 attribute comment : String]  
	:=  
	[[title    , comment                  ] 
	 ["Awesome", "Very pretty and useful"]]
			 
	UIWeb.ClickOnElement ("css:img.logo")
	UIWeb.ClickOnElement ("css:ul.product_list.homefeatured li.ajax_block_product.first-in-line.first-item-of-tablet-line div.product-image-container")
	UIWeb.ClickOnElement ("css:p.align_center span")
	UIWeb.CheckValueOnElement ("//*[@id=\"id_new_comment_form\"]/h2", "Write a Review")
	UIWeb.ClickOnElement ("css:div.star:nth-of-type(5)", "a")
	UIWeb.PostData ("css:input#comment_title", @review.title[1])
	UIWeb.PostData ("css:textarea", @review.comment[1])
	UIWeb.ClickOnButton ("//*[@id=\"submitNewMessage\"]")
	UIWeb.ClickAndWaitUntilElementIsVisible ("//*[@id=\"product\"]/div[2]/div/div/div/p[1]", "Your comment has been added and will be")
	UIWeb.CheckElementOnPage ("//*[@id=\"product\"]/div[2]/div/div/div/p[1]","Your comment has been added and will be available once approved by a moderator")
	UIWeb.ClickOnElement ("//*[@id=\"product\"]/div[2]/div/div/div/p[2]/button")
endRobot 

Robot Search_Products1 [description "As a User I want to search for a product by name or descripton" ]
begin  
	variable @sProducts1 : DataTable definedAs
	[attribute search1 : String,
	 attribute expectedResult1 : Integer]  
	:=   
	[[search1    , expectedResult1]  
	 ["Blouse123", 1]] 
	
	UIWeb.ClickOnElement ("css:img.logo")
	UIWeb.PostData ("css:input.search_query", @sProducts1.search1[1])
	UIWeb.ClickOnButton ("css:div.col-sm-4 button.btn")
	UIWeb.CheckValueOnElement ("//*[@id=\"center_column\"]/ul/li", @sProducts1.expectedResult1[1])	
endRobot
  
Robot Search_Products2 [description "As a User I want to search for a product by name or descripton" ]
begin 
	variable @sProducts2 : DataTable definedAs
	[attribute search2 : String,
	 attribute expectedResult2 : Integer]  
	:=    
	[[search2 , expectedResult2]  
	 ["Summer", 4]]
		   
	UIWeb.ClickOnElement ("css:img.logo") 
	UIWeb.PostData ("css:input.search_query", @sProducts2.search2[1])
	UIWeb.ClickOnButton ("css:div.col-sm-4 button.btn")
	UIWeb.CheckValueOnElement ("//*[@id=\"center_column\"]/ul/li", @sProducts2.expectedResult2[1])  
endRobot


